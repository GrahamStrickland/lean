def bufsize : USize := 20 * 1024

partial def dump (stream : IO.FS.Stream) : IO Unit := do
  let buf ← stream.read bufsize
  if buf.isEmpty then
    pure ()
  else
    let stdout ← IO.getStdout
    stdout.write buf
    dump stream

def fileStream (filename : System.FilePath) : IO (Option IO.FS.Stream) := do
  let fileExists ← filename.pathExists
  if not fileExists then
    let stderr ← IO.getStderr
    stderr.putStrLn s!"File not found: {filename}"
    pure none
  else
    let handle ← IO.FS.Handle.mk filename IO.FS.Mode.read
    pure (some (IO.FS.Stream.ofHandle handle))

def process (exitCode : UInt32) (args : List String) : IO UInt32 := do
  match args with
  | [] => pure exitCode
  | "-" :: args =>
    let stdin ← IO.getStdin
    dump stdin
    process exitCode args
  | filename :: args =>
    let stream ← fileStream ⟨filename⟩
    match stream with
    | none =>
      process 1 args
    | some stream =>
      dump stream
      process exitCode args

def printHelp : IO Unit := do
  let stdout ← IO.getStdout
  stdout.putStrLn "feline [<filenames>, ... | '-', --help]"
  stdout.putStrLn "<filenames> - Any number of filenames"
  stdout.putStrLn "'-'         - Redirect stdin to stdout"
  stdout.putStrLn "--help      - Print usage to stdout"

def main (args : List String) : IO UInt32 :=
  match args with
  | [] => process 0 ["-"]
  | x :: xs => if x == "--help" then do
    printHelp 
    process 0 xs
  else 
    process 0 args
